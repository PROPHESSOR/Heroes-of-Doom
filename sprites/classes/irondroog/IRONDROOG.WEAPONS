/**
 *Copyright (c) 2018 PROPHESSOR
 *
 *This software is released under the MIT License.
 *https://opensource.org/licenses/MIT
 */

// Arms in the hands of Iron Droog

Actor IronDroogFist : Fist {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Inventory.PickupSound "irondroog/pickup"

    States {
        Ready:
            MUPX A 1 A_WeaponReady
            Loop
        Deselect:
            MUPX A 1 A_Lower
            Loop
        Select:
            MUPX A 1 A_Raise
            Loop
        Fire:
            MUPX B 4
            MUPX C 5 A_CustomPunch(10, false, 0, null, 64, 0, 0, null, "irondroog/boom")
            MUPX B 4
            MUPX A 4
            MUPX D 4 A_CustomPunch(10, false, 0, null, 64, 0, 0, null, "irondroog/boom")
            MUPX E 5
            MUPX D 4
            MUPX A 5 A_ReFire
            Goto Ready
    }
}

Actor IronDroogKick : Kick {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Weapon.Kickback 500
    Weapon.SelectionOrder 1
    Inventory.PickupSound "irondroog/pickup"

    States {
        Ready:
            KICK A 0 A_WeaponReady
            Goto Fire
        Deselect:
            KICK A 0 A_Lower
            Loop
        Select:
            KICK A 0 A_Raise
            Loop
        Fire:
            KICK B 1
            KICK C 1
            KICK D 1
            KICK E 1 A_PlaySound("weapon/kick", CHAN_5, 1.0, 0.0)
            KICK F 1
            KICK G 1
            KICK H 4 A_CustomPunch(20, false, 0, "BulletPuff", 64, 0, 0, "", "irondroog/kick", "weapon/kickmiss")
            KICK I 3 A_Recoil(2)
            KICK G 1
            KICK B 1 A_TakeInventory("IronDroogKick")
            Stop
    }
}

Actor IronDroogChainsaw : Chainsaw {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Inventory.PickupSound "irondroog/pickup"

    States {
        Select:
            SAWG A 0 A_PlaySound("irondroog/weapons/chainsaw/up", CHAN_6)
            SAWG A 1 A_Raise
            Goto Select+1
        Deselect:
            SAWG A 0 A_StopSound(CHAN_7)
            SAWG A 1 A_Lower
            Loop
        Fire:
            SAWG D 9 A_PlaySound("irondroog/weapons/chainsaw/up", CHAN_6)
            Goto Hold
        Hold:
            SAWG A 0 A_PlaySound("irondroog/weapons/chainsaw/fire", CHAN_7, 1.0, 1.0)
            SAWG AB 1 A_Saw(null, "irondroog/weapons/chainsaw/fire")
            SAWG B 0 A_ReFire
            SAWG C 0 A_StopSound(CHAN_7)
            Goto Ready

    }
}

Actor IronDroogPistol : Pistol {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Inventory.PickupSound "irondroog/pickup"
}

Actor IronDroogShotgun : Shotgun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Inventory.PickupSound "irondroog/pickup"

    States {
        Fire:
            SHTG A 3
            SHTG A 0 A_FireBullets (5.6, 0, 7, 15, "BulletPuff")
            SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
            SHTG A 5 A_GunFlash
            SHTG BC 5
            SHTG D 4
            SHTG CB 5
            SHTG A 3
            SHTG A 7 A_ReFire
            Goto Ready
    }
}

Actor IronDroogSSG : SuperShotgun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Weapon.Kickback 200
    Weapon.AmmoUse 4
    Inventory.PickupSound "irondroog/pickup"

    States {
        Select:
            LSEL ABCDEFGHIJKLMNNNN 1 A_Raise
            Loop
        Deselect:
            LSEL NMLKJIHGFEDCBAAAA 1 A_Lower
            Loop
        Ready:
            LCSG A 1 A_WeaponReady
            Loop
        Fire:
            LCSG A 3
            LCSG A 0 A_FireBullets (11.2, 7.1, 40, 15, "BulletPuff")
            LCSG A 0 A_PlaySound ("irondroog/weapons/ssg/hit", CHAN_WEAPON)
            LCSG X 3 Bright A_SetPitch(Pitch - 5.0 - FRandom(0.0, 5.0))
            LCSG Y 4 Bright A_Recoil(FRandom(3.5, 7.0))
            LCSG X 3 Bright A_Recoil(FRandom(-2.0, -7.0))
            LCSG A 8
            LCSG B 5
            LCSG C 4 A_CheckReload
            LCSG D 3 A_PlaySound ("weapons/sshoto", CHAN_7)
            LCSG E 3
            LCSG F 3
            LCSG G 2
            LCSG H 2
            LCSG I 2
            LCSG J 2
            LCSG K 2
            TNT1 AA 15 A_PlaySound ("weapons/sshotl", CHAN_6)
            LCSG L 1 A_PlaySound ("weapons/sshoto", CHAN_WEAPON)
            LCSG M 1
            LCSG N 3 A_PlaySound ("irondroog/weapons/ssg/load", CHAN_7)
            LCSG O 3
            LCSG A 3 A_ReFire
            Goto Ready
    }
}

Actor IronDroogChaingun : Chaingun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Inventory.PickupSound "irondroog/pickup"

    States {
        Ready:
            NCHA B 0 A_PlaySound("irondroog/weapons/chaingun/loop", CHAN_WEAPON, 1.0, 1.0)
            NCHA BC 1 A_WeaponReady
            Loop
        Deselect:
            NCHA B 0 A_PlaySound("irondroog/weapons/chaingun/stop", CHAN_WEAPON)
            NCHA B 1 A_Lower
            Goto Deselect+1
        Select:
            NCHA B 0 A_PlaySound("irondroog/weapons/chaingun/start", CHAN_WEAPON)
            NCHA B 1 A_Raise
            Goto Select+1
        Fire:
            NCHA D 0 A_ZoomFactor(FRandom(0.99, 0.999), ZOOM_INSTANT)
            NCHA D 0 A_SetPitch(Pitch - FRandom(-0.2, 0.7)) 
            NCHA D 0 A_SetAngle(Angle + FRandom(-0.4, 0.2))
            NCHA D 0 A_PlaySound("irondroog/weapons/chaingun/fire", CHAN_6)
            NCHA D 1 Bright A_FireBullets(6.66, 6.66, 1, 3, "BulletPuff")
            NCHA E 0 A_Recoil(0.03)
            NCHA E 0 A_SetPitch(Pitch - FRandom(-0.2, 0.4)) 
            NCHA E 0 A_SetAngle(Angle + FRandom(-0.2, 0.1))
            NCHA E 0 A_ZoomFactor(1)
            NCHA E 0 A_PlaySound("irondroog/weapons/chaingun/fire", CHAN_7)
            NCHA E 1 Bright A_FireBullets(6.66, 6.66, 1, 3, "BulletPuff")
            NCHA E 0 A_ReFire
            Goto Ready
    }
}

Actor IronDroogRL : RocketLauncher {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo  "IronDroog"
    Inventory.PickupSound   "irondroog/weapons/vcleaner/pickup"
    Weapon.UpSound          "irondroog/weapons/vcleaner/up"

    States {
        Deselect:
            MISG B 0 A_StopSound(CHAN_WEAPON)
            MISG A 1 A_Lower
            Goto Deselect+1
        Fire:
            MISG B 0 A_TakeInventory("RocketAmmo", 25)
            MISG B 0 A_PlaySound("irondroog/weapons/vcleaner/start", CHAN_7)
            MISG B 0 A_PlaySound("irondroog/weapons/vcleaner/loop", CHAN_WEAPON, 1, 1)
            Goto Hold
        Hold:
            MISG B 0 A_GunFlash
            MISG B 1 A_RadiusThrust(-5000, 20000, RTF_NOIMPACTDAMAGE)
            MISG B 1 A_Saw(null, null, 128, "Blood", SF_NOUSEAMMO)
            MISG B 0 A_ReFire
            MISG B 0 A_StopSound(CHAN_WEAPON)
            Goto Ready
    }
}

Actor IronDroogPlasmagun : PlasmaRifle {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Inventory.PickupSound "irondroog/weapons/prostate/upfirst"

    States {
        Ready:
            GORG ABCD 3 A_WeaponReady
            Loop
        Deselect:
            GORG A 0 A_StopSound(CHAN_WEAPON)
            GORG A 1 A_Lower
            Loop
        Select:
            GORG A 0 A_PlaySound("irondroog/weapons/prostate/up", CHAN_6)
            GORG A 1 A_Raise
            Goto Select+1
        Fire:
            GORG A 0 A_PlaySound("irondroog/jump", CHAN_5)
            GORG RQPONMLKJI 3
            Goto Hold
        Hold:
            GORG EFG 1
            GORG A 0 A_PlaySound("irondroog/weapons/prostate/fire", CHAN_WEAPON, 1.0, 1.0)
            GORG H 1 A_FireCustomMissile("ProstateBall", 0, 1)
            GORG A 0 A_ReFire
            GORG A 0 A_PlaySound("irondroog/jump", CHAN_5)
            GORG RQPONMLKJI 3
            GORG A 0 A_StopSound(CHAN_WEAPON)
            Goto Ready
        Flash:
            Goto LightDone
        Spawn:
            PLAS A -1
            Stop
    }
}

Actor ProstateBall : PlasmaBall {
    +SEEKERMISSILE

    SeeSound "irondroog/weapons/prostate/hit"
    DeathSound "irondroog/weapons/prostate/hit"
    Obituary "%o was melted by %k's IronDroog's prostate."

    States {
        Spawn:
            SPIR P 6 Bright A_SeekerMissile(360, 90, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
            Loop
        Death:
            SPIR QRSTU 4 Bright
            Stop
    }
}

#include "sprites/classes/irondroog/IRONDROOG.CUM"

Actor IronDroogBFG : Chainsaw {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "IronDroog"
    Weapon.UpSound ""

    States {
        Select:
            TNT1 A 50 A_PlaySound("irondroog/weapons/hfucker/up", CHAN_5)
            HFKR C 1 A_Raise
            Goto Select+1
        Deselect:
            HFKR C 1 A_Lower
            Loop
        Ready:
            TNT1 A 0 A_PlaySound("weapons/sawup", CHAN_WEAPON)
            HFKR CD 1 A_WeaponReady
            Goto Ready+1
        Fire:
            HFKR B 15 A_PlaySound("irondroog/weapons/hfucker/generic", CHAN_5)
            HFKR B 0 A_PlaySound("weapons/sawup", CHAN_WEAPON)
            Goto Hold
        Hold:
            HFKR B 0 A_Recoil(5)
            HFKR B 2 A_SpawnItemEx("Mayonez_Blood", 0, -10.0, 25.0)
            HFKR B 0 A_Recoil(-5)
            HFKR A 1 A_CustomPunch(250, false, 0, "CumSplat", 64, 0, 0, "", "weapons/sawhit", "")
            HFKR AA 1 A_SpawnItemEx("Mayonez_FlyingBloodTrail", -10, 0, 25, 5, 0, 2)
            HFKR A 1 A_Saw
            TNT1 A 0 A_Refire
            
            Goto Ready
    }
}
