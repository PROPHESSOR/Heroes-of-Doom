/**
*Copyright (c) 2018 PROPHESSOR
*
*This software is released under the MIT License.
*https://opensource.org/licenses/MIT
*/

// Arms in the hands of Nomakh Thunder

Actor NomakhFist : Fist {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhKick : Kick {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Weapon.Kickback 350
    Inventory.PickupSound "nomakh/pickup"
    Weapon.SelectionOrder 1

    States {
        Ready:
            KICK A 0 A_WeaponReady
            Goto Fire
        Deselect:
            KICK A 0 A_Lower
            Loop
        Select:
            KICK A 0 A_Raise
            Loop
        Fire:
            KICK B 1
            KICK C 1
            KICK D 1
            KICK E 1 A_PlaySound("weapon/kick", CHAN_5, 1.0, 0.0)
            KICK F 1
            KICK G 1 // A_CustomPunch(0, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown")
            KICK H 3
            KICK H 1 A_CustomPunch(20, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown", "weapon/kickmiss")
            KICK I 3
            KICK G 1 A_Recoil(2)
            KICK B 1 ACS_NamedExecuteAlways("ReturnKick", 0)
            KICK B 0 A_TakeInventory("NomakhKick")
            Stop
    }
}

Actor NomakhChainsaw : Chainsaw {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhPistol : Pistol {
    +INVENTORY.RESTRICTABSOLUTELY
    +NOALERT

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"

    Tag "Silver Hornet"
    Inventory.PickupSound "misc/w_pkup"
    Inventory.PickupMessage "Picked up the revolver"
    Obituary "%k mistook %o with a tree."

    Weapon.UpSound "nomakh/weapons/pistol/up"
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 0
    Weapon.AmmoUse 5
    Weapon.AmmoType2 "Clip"
    Weapon.Kickback 100
    Weapon.SlotNumber 2

    States {
        Ready:
            HRNG A 1 A_WeaponReady
            Loop
        Deselect:
            TNT1 A 0 A_ZoomFactor(1)
            HRNG A 1 A_Lower
            HRNG A 0 A_Lower
            Loop
        Select:
            HRNG AAAAAAA 0 A_Raise(12)
            HRNG ABCABC 2
            Loop
        Fire:
            HRNF A 0 A_PlaySound("nomakh/weapons/pistol/fire",0)
            HRNF A 0 A_Recoil(1)
            HRNF A 0 A_ZoomFactor(0.7)

            HRNF A 0 A_FireBullets(2.0, 0.75, 1, 60, "BulletPuff")

            HRNF A 0 A_Light2
            HRNF A 0 A_ZoomFactor(0.98)
            HRNF A 1 Bright A_SetPitch(pitch-0.75)
            HRNF A 0 Bright A_SetAngle(angle-0.75)
            HRNF A 0 A_ZoomFactor(0.985)
            HRNF B 1 Bright A_SetPitch(pitch-0.5)
            HRNF B 0 Bright A_SetAngle(angle-0.5)
            PISF A 0 A_Light1
            HRNR A 1 A_ZoomFactor(0.99)
            HRNR B 1 A_ZoomFactor(0.995)
            HRNR C 1 A_ZoomFactor(1)
            HRNR C 0 A_Light0
            HRNR CCDD 1
            HRNG C 1
            HRNG B 1
            HRNG A 3 A_ReFire
            Goto Ready
        Spawn:
            PIST A -1
            Stop
    }
}

Actor NomakhShotgun : Shotgun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"

    States {
        Ready:
            SHTI ABCDEFGHIJK 1 A_WeaponReady
            Loop
        Deselect:
            SHSS ABCD 1
            SHSS AAAAAAAAAA 0 A_Lower(12)
            Loop
        Select:
            TNT1 A 0 A_PlaySound("nomakh/shotgunwitheggs", CHAN_VOICE)
            SHSS AAAAAAAAAA 0 A_Raise(12)
            SHSS DCBA 1
            Loop
        Fire:
            SHTF A 2
            SHTF B 3 A_FireShotgun
            SHTF CDE 2
            SHTG ABCDEFGHIJKLMNODCB 1
            SHTG A 5 A_ReFire
            Goto Ready
        Spawn:
            SHTC A -1
            Stop
    }
}

/*
    CREDITS:
    Random Name Generator - this gun name is actually quite spiffy!
    Original Quadshot - Logan
    Beatiful angled Quadshot sprite - Cage!i!i!i!i!i! (added those !'s and i's on his special request - I owe him after all. :P)
    NeuralStunner - Three-dimensional recoil
    Fire sound - Propably Killing Floor, but I'm not sure. Ripped by Uboa. Great thanks for that, it adds insane amount of kick to the gun!
    I am not quite sure from where are rest sounds; which game are the original sounds, but those are modified ones from NeoCore. Ripped by Sangelothi.
    Gunflash - Bane, props to you for this one - it's really damn nice gunflash - it became my new staple one.

    Compared to Erasus, it's slower in terms of fire rate, and it doesn't have casing-spewing attachment. It also has horrible recoil,
    But that can be used in positive way, since you can use it as a make-shift rocket-jumping device. It also makes up with it's
    flaws by shooting twice as much pellets per shot, with even bigger power, and slightly tightened horizontal spread.
    Also, it has a nasty ability to deal horribly large damage on point-blank shot. Give Cyberdemon a taste!
*/

Actor NomakhSSG : SuperShotgun {
    +INVENTORY.RESTRICTABSOLUTELY
    +NOALERT

    Weapon.Kickback 1000
    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"
    Inventory.PickupMessage "You've got the quad shotgun!"
    Weapon.UpSound "nomakh/weapons/ssg/Up"
    Weapon.AmmoType "Shell"
    Weapon.AmmoUse 16
    Weapon.AmmoGive 16
    Weapon.SlotNumber 3
    Obituary "%o was blown away and smeared on wall by %k using Nomakh's 4x4 shotgun."

    States {
        Spawn:
            SSHG X -1
            Stop
        Ready:
            SSH4 B 0 A_TakeInventory("NomakhSSGReloadCounter", 4)
            SSH4 B 1 A_WeaponReady
            Loop
        Select:
            SSH4 BBBBBBBBBB 0 A_Raise(12)
            SSH4 MKIGEC 1
            Loop
        Deselect:
            SSHG A 0 A_ZoomFactor(1)
            SSH4 BDFHJL 1
            SSH4 MMMMMMMM 0 A_Lower(12)
            Loop
        Fire:
            TNT1 A 0 A_PlaySound("nomakh/weapons/ssg/Deploy",5,0.65)

            SSH4 A 15

            SSF4 A 2 Bright {
                A_AlertMonsters;
                A_ChangeVelocity(Cos(Pitch) * -116, 0, Sin(Pitch) * 116, CVF_RELATIVE);
                A_Quake(9, 18, 0, 64, NONE);
                A_PlaySound("nomakh/weapons/ssg/Fire", 1);
                A_PlaySound("nomakh/weapons/ssg/Alt", 6);
                A_PlaySound("nomakh/boom", CHAN_VOICE);
                A_FireBullets(25, 7.5, 128, 15, "BulletPuff");
            }

            SSH4 B 1 {
                A_ZoomFactor(0.9);
                A_SetPitch(Pitch - 6);
            }
            SSH4 D 1 {
                A_ZoomFactor(0.8);
                A_SetPitch(Pitch - 4);
            }
            SSH4 F 1 {
                A_ZoomFactor(0.7);
                A_SetPitch(Pitch - 1);
            }
            SSH4 H 1 A_ZoomFactor(0.5)
            SSH4 J 1 A_ZoomFactor(0.3)
            SSH4 LM 1
            TNT1 A 7 A_ZoomFactor(1)
            SSHO RQPONMLKJIHGF 1
            SSHO F 1
            SSHO EDCBA 1
            SSHN GFEDCBA 1
            SSHG A 0 A_CheckReload
            SSHG A 4 A_PlaySound("nomakh/cybr", CHAN_7, 1.0)
            Goto Reload
        Reload:
            SSHN ABDEG 1
            SSHO ABDEIK 1
            SSHO M 1 A_PlaySound("nomakh/weapons/ssg/Open", CHAN_5, 0.3)
            SSHO NOQRTUWX 1
            SSHL A 3
            SSHL BCDE 1
            SSHL F 1 A_PlaySound("nomakh/weapons/ssg/Load", CHAN_6, 0.2)
            SSHL GHIJ 1
            SSHL K 2
            SSHL LMNO 1
            SSHL P 1 A_PlaySound("nomakh/weapons/ssg/Load", CHAN_5, 0.2)
            SSHL QRSTU 1
            SSHL V 2
            SSHC ABDE 1
            SSHO R 0 A_PlaySound("nomakh/weapons/ssg/Close", CHAN_6, 0.3)
            SSHO RQONKI 1
            SSHN GFDCB 1
            SSHG A 2 A_GiveInventory("NomakhSSGReloadCounter", 1)
            SSHG A 0 A_JumpIfInventory("NomakhSSGReloadCounter", 4, "Ready")
            Loop
        }
}

Actor NomakhSSGReloadCounter : Inventory {
    Inventory.MaxAmount 4
}

Actor NomakhChaingun : Chaingun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"
    Weapon.AmmoUse 2

    States {
        Ready:
            NHGG A 1 A_WeaponReady
            Loop
        Deselect:
            NHGG A 1 A_Lower
            Loop
        Select:
            NHGG A 1 A_Raise
            Loop
        Fire:
            NHGG AB 8 A_PlaySound("nomakh/weapons/chaingun/start")
            NHGG AB 6 A_PlaySound("nomakh/weapons/chaingun/start")
            NHGG AB 4 A_PlaySound("nomakh/weapons/chaingun/start")
            NHGG ABAB 3
            NHGG ABAB 2
            Goto Hold
        Hold:
            NHGG A 0 A_PlaySound("nomakh/weapons/chaingun/fire", CHAN_WEAPON)
            NHGG A 0 A_GunFlash
            NHGG B 0 A_Recoil(0.5)
            NHGG B 0 A_SetAngle(Angle + FRandom(-0.5, 0.5))
            NHGG B 0 A_SetPitch(Pitch + FRandom(-0.35, 0.35))
            NHGG A 2 A_FireBullets(6.6, 0, 3, 15, "BulletPuff")
            NHGG B 0 A_Recoil(0.5)
            NHGG B 0 A_SetAngle(Angle + FRandom(-0.5, 0.5))
            NHGG B 0 A_SetPitch(Pitch + FRandom(-0.35, 0.35))
            NHGG B 0 A_PlaySound("nomakh/weapons/chaingun/fire", CHAN_WEAPON)
            NHGG B 0 A_GunFlash("Flash2")
            NHGG B 2 A_FireBullets(6.6, 0, 3, 15, "BulletPuff")
            NHGG B 0 A_GiveInventory("Clip", 1)
            NHGG B 0 A_Recoil(0.5)
            NHGG B 0 A_SetAngle(Angle + FRandom(-0.5, 0.5))
            NHGG B 0 A_SetPitch(Pitch + FRandom(-0.35, 0.35))
            NHGG B 0 A_ReFire
            Goto AfterHold
        AfterHold:
            NHGG AB 2 A_PlaySound("nomakh/weapons/chaingun/stop")
            NHGG AB 2
            NHGG A 4
            NHGG B 6 A_PlaySound("nomakh/weapons/chaingun/stop")
            NHGG A 8
            NHGG B 8 A_PlaySound("nomakh/weapons/chaingun/stop")
            Goto Ready
        Flash:
            NHGF A 4 Bright A_Light1
            Goto LightDone
        Flash2:
            NHGF B 4 Bright A_Light2
            Goto LightDone
        Spawn:
            MGUN A -1
            Stop
    }
}

Actor NomakhRL : RocketLauncher {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"

    States {
        Ready:
            MI6R A 1 A_WeaponReady
            Loop
        Deselect:
            MI6R A 1 A_Lower
            Loop
        Select:
            MI6R A 1 A_Raise
            Loop
        Fire:
            MI6R B 8 A_GunFlash
            MI6R B 12 A_FireProjectile("NomakhRocket")
            MI6R B 0 A_ReFire
            Goto Ready
        Flash:
            MI6F A 3 Bright A_Light1
            MI6F B 4 Bright
            MI6F CD 4 Bright A_Light2
            Goto LightDone
    }
}

Actor NomakhRocket : Rocket {
    States {
        Spawn:
            MI6L A 10 Bright A_SetScale(1.5)
            MI6L A 0 Bright SetDamage(30)
            MI6L A 10 Bright A_SetScale(2.0)
            MI6L A 0 Bright SetDamage(40)
            MI6L A 10 Bright A_SetScale(2.5)
            MI6L A 0 Bright SetDamage(50)
            MI6L A 10 Bright A_SetScale(3.0)
            MI6L A 0 Bright SetDamage(60)
            MI6L A 10 Bright A_SetScale(3.5)
            MI6L A 0 Bright SetDamage(70)
            MI6L A 10 Bright A_SetScale(4.0)
            MI6L A 0 Bright SetDamage(80)
            MI6L A 10 Bright A_SetScale(4.5)
            MI6L A 0 Bright SetDamage(90)
            MI6L A 10 Bright A_SetScale(5.0)
            MI6L A 0 Bright SetDamage(100)
            MI6L A 10 Bright A_SetScale(5.5)
            MI6L A 0 Bright SetDamage(110)
            MI6L A 10 Bright A_SetScale(6.0)
            MI6L A 0 Bright SetDamage(120)
            MI6L A 10 Bright A_SetScale(6.5)
            MI6L A 0 Bright SetDamage(130)
            Goto SpawnLoop
        SpawnLoop:
            MI6L A 1 Bright
            Loop
        Death:
            MI6L B 8 Bright A_Explode
            MI6L C 6 Bright
            MI6L D 4 Bright
            Stop
    }
}

Actor NomakhPlasmagun : PlasmaRifle {
    +INVENTORY.RESTRICTABSOLUTELY
    +WEAPON.NOAUTOAIM
    +WEAPON.NOAUTOFIRE
    +WEAPON.NOALERT

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"
    Weapon.AmmoUse 30
    Weapon.AmmoGive 20

    DamageType Railgun

    States {
        Ready:
            RAIL ABC 1 A_WeaponReady
            Loop
        Deselect:
            RAIL ABC 1 A_Lower
            Loop
        Select:
            RAIL ABC 1 A_Raise
            Loop
        Fire:
            TNT1 A 0 A_GunFlash
            RAIL D 3 A_RailAttack(512, 0, 1, "00 FF FF", "00 85 FF", 0, 0, "none", 0, 0, 0, 0, 1.0, 2.0, "none", 5.0)
            RAIL D 10
            RAIL ABCA 5
            Goto Ready
        Flash:
            RAIF AB 4 Bright A_Light1
            Goto LightDone
            RAIF BA 4 Bright A_Light1
            Goto LightDone
        Spawn:
            PLAS A -1
            Stop
    }
}

Actor NomakhBFG : BFG9000 {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "Nomakh"
    Inventory.PickupSound "nomakh/pickup"
}
