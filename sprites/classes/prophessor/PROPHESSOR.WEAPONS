/**
*Copyright (c) 2018 PROPHESSOR
*
*This software is released under the MIT License.
*https://opensource.org/licenses/MIT
*/

// Arms in the hands of PROPHESSOR

Actor ProphessorFist : Fist {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.PickupSound "prophessor/pickup"
}

Actor ProphessorKick : Kick {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Weapon.Kickback 228
    Inventory.PickupSound "prophessor/pickup"

    States {
        Ready:
            KICK J 0 A_WeaponReady
            Goto Fire
        Deselect:
            KICK J 0 A_Lower
            Loop
        Select:
            KICK J 0 A_Raise
            Loop
        Fire:
            KICK K 1
            KICK L 1
            KICK M 1
            KICK N 1 A_PlaySound("weapon/kick", CHAN_5, 1.0, 0.0)
            KICK O 1
            KICK P 1 A_CustomPunch(0, false, 0, "BulletPuff", 64, 0)
            KICK P 0 A_CheckLOF(2)
            KICK P 0 A_Jump(256, 2)
            KICK P 0 A_PlayWeaponSound("weapon/kickdown")
            KICK Q 1 A_CustomPunch(11, false, 0, "BulletPuff", 64, 0)
            KICK Q 0 A_CheckLOF(3)
            KICK Q 0 A_PlayWeaponSound("weapon/kickmiss")
            KICK Q 0 A_Jump(256, 2)
            KICK Q 0 A_PlayWeaponSound("weapon/kickdown")
            KICK R 3 A_Recoil(4)
            KICK S 1 
            KICK T 1 A_TakeInventory("ProphessorKick")
            Stop
    }
}

Actor ProphessorChainsaw : Chainsaw {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.PickupSound "prophessor/pickup"

    Weapon.AmmoType "ProphessorChainsawAmmo"
    Weapon.AmmoUse 1

    States {
        Ready:
            TSWG A 0 A_JumpIfNoAmmo("NoAmmo")
            TSWG A 2 A_WeaponReady
            TSWG B 2 A_Saw("prophessor/weapons/chainsaw/loop", "weapons/sawhit", 5, null, SF_NOUSEAMMO)
            TSWG C 2 A_WeaponReady
            TSWG E 2 A_Saw("prophessor/weapons/chainsaw/loop", "weapons/sawhit", 5, null, SF_NOUSEAMMO)
            TSWG F 2 A_WeaponReady
            Loop
        Select:
            TSWG A 0 A_PlaySound("prophessor/weapons/chainsaw/up")
            TSWG D 1 A_Raise
            Goto Select+1
        Deselect:
            TSWG A 0 A_PlaySound("prophessor/weapons/chainsaw/down")
            TSWG D 1 A_Lower
            Goto Deselect+1
        Fire:
            TSWG A 0 A_JumpIfNoAmmo("NoAmmo")
            TSWG A 0 A_PlaySound("weapons/sawhit")
            TSWG A 0 A_FireCustomMissile("ProphessorChainsawDisk", 0, 1, 0, 3)
            TSWG DG 4
            Goto Ready
        NoAmmo:
            TSWG D 2 A_JumpIfInventory("ProphessorChainsawAmmo", 1, "Ready")
            TSWG G 2 A_WeaponReady 
            Loop
    }
}

Actor ProphessorChainsawAmmo : Ammo {
    +INVENTORY.RESTRICTABSOLUTELY
    
    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.MaxAmount 16

    States {
        Spawn:
            SAWS A -1
            Wait
    }
}

// Code from Infernal Attack
Actor ProphessorChainsawDisk {
    +NOGRAVITY
    +DROPOFF
    +MISSILE
    +ACTIVATEIMPACT
    +ACTIVATEPCROSS
    +SEEKERMISSILE
    +RIPPER
    +EXTREMEDEATH
    +NOEXPLODEFLOOR

    Radius 4
    Height 2 
    Speed 20
    Scale 0.3
    Damage 3
    ExplosionDamage 16
    ExplosionRadius 8
    SeeSound "prophessor/weapons/chainsaw/launch"
    DeathSound "prophessor/weapons/chainsaw/hit"
    
    States {
        Spawn:
            SAWS AABBCCDDEEFFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 2 A_SeekerMissile(90,90) //FIXME: WTF?!
            SAWS A 0 A_Gravity
            SAWS AABBCCDDEEFFGGHH 2 A_SeekerMissile(90,90)
            Goto Spawn+66
        Crash:
            SAWE A 1 A_ChangeFlag("RIPPER",0)
        Death:
            SAWE BCDEFG 1
            SAWE B 0 A_SpawnItemEx("ProphessorChainsawAmmo")
            Stop
    }
}

Actor ProphessorPistol : Pistol {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Weapon.AmmoUse 100

    States {
        Ready:
            UTIL A 1 A_WeaponReady
            Loop
        Deselect:
            UTIL A 1 A_Lower
            Loop
        Select:
            UTIL A 1 A_Raise
            Loop
        Flash:
            TNT1 A 7 Bright A_Light1
            Goto LightDone
            TNT1 A 7 Bright A_Light1
            Goto LightDone
        Fire:
            UTIL A 4 A_PlaySound("prophessor/weapons/pistol/build")
            UTIL B 1 A_FireBullets (5.6, 0, 1, 512, "BulletPuff")
            UTIL A 1 A_GunFlash
            UTIL B 15 A_SpawnItemEx("Backpack", 100, 0, 10, random(10, 20), 0, 0)
            UTIL A 4 A_ReFire
            Goto Ready
    }
}

Actor ProphessorShotgun : Weapon {
    +NOALERT
    +INVENTORY.RESTRICTABSOLUTELY
    +WEAPON.AMMO_OPTIONAL

    Scale 0.85

    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.PickupSound "prophessor/pickup"
    Inventory.PickupSound "prophessor/pickup"
    Inventory.PickupMessage "Shotgun"

    Weapon.SelectionOrder 4500
    Weapon.KickBack 80
    Weapon.AmmoUse 1
    Weapon.AmmoGive 8
    Weapon.AmmoType "Shell"
    Obituary "%o was blown away by %k using PROPHESSOR's Shotgun."

    States {
        Ready:
            SMAG A 1 A_WeaponReady
            Loop
        Deselect: 
            SMAG A 1 A_Lower
            Loop
        Select:
            TNT1 AAAAAAAAAAAAAA 0 A_Raise
            ASGW A 1
            ASGR ABCDE 1
            Loop
        Fire:
            TNT1 A 0 A_JumpIfNoAmmo("Empty")
            TNT1 A 0 A_PlaySound("prophessor/weapons/shotgun/fire", CHAN_WEAPON)
            TNT1 A 0 A_AlertMonsters
            TNT1 A 0 Radius_Quake(3, 3, 0, 1, 0)
            TNT1 A 0 A_GunFlash
            TNT1 A 0 A_Light1
            SMAF A 2 Bright A_FireBullets(5.0, 3.0, 7, Random(5, 7), "BulletPuff")
            SMAF B 2 Bright A_Light2
            TNT1 A 0 A_Light0
            SMAG BCDE 1
            SMAG FA 2
            TNT1 A 0 A_ReFire
            Goto Reload
        Empty:
            SMAG A 10 A_PlaySound("prophessor/weapons/shotgun/click", CHAN_WEAPON)
            Goto Ready
        Reload:   
            SMAK A 4
            SMAK B 4
            SMAK C 4
            SMAK D 4 A_PlaySound("prophessor/weapons/shotgun/reload", CHAN_WEAPON)
            SMAK E 4
            Goto Ready
        Spawn:
            SHOT A -1
            Stop
    }
}

Actor ProphessorSSG : SuperShotgun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.PickupSound "prophessor/pickup"
}

Actor ProphessorChaingun : Chaingun {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.PickupSound "prophessor/pickup"

    States {
        Fire:
            CHGG AB 2 A_FireCGun
            CHGG B 0 A_ReFire
            Goto Ready
    }
}

Actor ProphessorRL : RocketLauncher {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Inventory.PickupSound "prophessor/pickup"

    States {
        Fire:
            MISG B 8 A_GunFlash
            MISG B 0 A_FireMissile
            MISG B 12 A_FireMissile
            MISG B 0 A_ReFire
            Goto Ready
    }
}

Actor ProphessorPlasmagun : PlasmaRifle {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Weapon.AmmoUse 1
    Inventory.PickupSound "prophessor/pickup"

    States {
        Ready:
            MUS1 A 1 A_WeaponReady
            Loop
        Deselect:
            MUS1 A 1 A_Lower
            Loop
        Select:
            MUS1 A 1 A_Raise
            Loop
        Fire:
            MUS1 B 1 A_FirePlasma
            MUS1 CBCB 0 Bright A_Recoil( FRandom( 0.02, 0.35 ) )
            MUS1 C 0 A_ReFire
            Goto Ready
        Flash:
            MUS1 B 4 Bright A_Light1
            Goto LightDone
            MUS1 C 4 Bright A_Light1
            Goto LightDone
        Spawn:
            PLAS A -1
            Stop
    }
}

Actor ProphessorBFG : BFG9000 {
    +INVENTORY.RESTRICTABSOLUTELY

    Inventory.RestrictedTo "PROPHESSOR"
    Weapon.AmmoUse 250
    Inventory.PickupSound "prophessor/pickup"

    States {
        Ready:
            WTFG A 1 A_WeaponReady
            Loop
        Deselect:
            WTFG A 1 A_Lower
            Loop
        Select:
            WTFG A 1 A_Raise
            Loop
        Fire:
            WTFG A 0 A_PlaySound("prophessor/ahrinet", CHAN_6, 1.0)
            WTFG A 0 A_BFGSound
            WTFG ABCDEFGHIJKLMNOPQ 2
            WTFG RST 4 A_GunFlash
            WTFG U 1 A_FireProjectile("BRG9000Ball", 0, true, 0, 1)
            BFGF B 0 Bright A_SetBlend("FFFFFF", 0.5, 1)
            BFGF B 0 Bright A_GiveInventory("TimeFreeze")
            BFGF B 0 Bright A_SpawnItemEx("TimeFreezeSound")
            BFGF ACE 1 Bright A_Quake(6, 6, 0, 0)
            BFGF B 0 Bright A_SetBlend("FF4444", 0.5, 516)
            WTFG U 516
            WTFG B 0 A_TakeInventory("TimeFreeze")
            WTFG QPONMLKJIHGFEDCB 2
            Goto Ready
    }
}

// Big Research Gun =D
Actor BRG9000Ball {
    PROJECTILE
    +FORCERADIUSDMG
    +THRUSPECIES
    +NOTIMEFREEZE
    +SOLID
    +SHOOTABLE

    Speed 0
    Radius 20
    Height 30
    Damage 500
    Alpha 0.95
    Scale 1.5
    Alpha 0.75
    DeathSound "weapons/bfgx"
    Species "Player"
    Obituary "$OB_MPBFG_BOOM"
    RenderStyle Add
    Translation "112:127=172:191", "224:231=168:171"

    States {
        Spawn:
            TNT1 A 0 A_AlertMonsters
            BFS1 ABAD 1 Bright A_SpawnItemEx("BRGResearchRail", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
            Goto Flying
        Flying:
            TNT1 A 0 A_ChangeVelocity(0, 0, -0.1, CVF_REPLACE)
            TNT1 A 0 A_AlertMonsters
            TNT1 A 0 A_PlaySoundEx("prophessor/weapons/bfg/ball", "Voice", 1) // TODO: Плохо обращаться к звукам других классов
            BFS1 AB 1 Bright A_SpawnItemEx("BRGResearchRail", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
            Loop
        Death:
            TNT1 A 0 A_SetTranslucent(0.75, 1)
            TNT1 A 0 Radius_Quake(5, 60, 0, 10, 0)
            BFS1 A 0 A_PlaySoundEx("weapons/bfgx", "Voice", 0)
            BFE1 ABCDEF 4 Bright A_Explode(50, 128)
            TNT1 A 25 Bright
            Stop
    }
}

Actor BRGResearchRail {
    PROJECTILE
    +LOOKALLAROUND
    +ISMONSTER
    +QUICKTORETALIATE
    +NOTARGET
    +FRIENDLY
    +NOCLIP
    +NOTIMEFREEZE
    +FOILINVUL
    +FOILBUDDHA

    Obituary "%o was researched by %k with PROPHESSOR's research rail"

    States {
        Spawn:
            TNT1 A 0
            TNT1 AAAAA 1 A_Look
            Stop
        See:
            TNT1 A 0
            TNT1 A 1 A_JumpIfCloser(1250, "Attack")
            Stop
        Attack:
            TNT1 A 0
            TNT1 A 0 A_FaceTarget
            TNT1 A 0 A_MissileAttack
            TNT1 AAA 1 A_CustomRailgun(10, 0, "Green", "Yellow", RGF_FULLBRIGHT, 1, 2)
            Goto Death
        Death:
            TNT1 A 1
            Stop
    }
}
